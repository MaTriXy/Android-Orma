buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.realm:realm-gradle-plugin:3.3.2'
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.github.gfx.android.orma.example"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "Orma ${rootProject.ext.versionName} (release)"
        }
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "Orma ${rootProject.ext.versionName} (release)"
        }
    }
    productFlavors {
        normal {}
        encrypted {
            applicationIdSuffix ".encrypted"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        disable 'SetTextI18n'
        disable 'UnusedResources'
        disable 'HardcodedText'
        disable 'RtlSymmetry'
        disable 'RtlHardcoded'
        disable 'IconLocation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    dexOptions {
        preDexLibraries = !System.getenv("CI")
    }
}

dependencies {
    annotationProcessor project(':processor')
    compile project(':library')
    compile project(':encryption')
    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'
    compile 'com.facebook.stetho:stetho:1.4.2'
}
